apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace 'com.miaad.iso8583TCPSocket'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Provide sources JAR in AGP 4.2.x
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.6.0'
}

// Configure Maven publication for JitPack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = project.findProperty('group') ?: 'com.github.Miaadrajabi'
                artifactId = 'iso8583TCPSocket'
                version = project.findProperty('version') ?: '1.0.0-SNAPSHOT'
            }
        }
    }
}